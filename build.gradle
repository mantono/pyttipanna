group 'com.mantono'
version '0.1.3'
description = 'description of project'

defaultTasks 'test'

buildscript {
    ext.kotlin_version = '1.2.21'
    ext.jvm_version = '1.8'
    ext.junit_platform_version = '1.1.0'

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_platform_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'idea'
apply plugin: "org.junit.platform.gradle.plugin"

sourceCompatibility = jvm_version

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8', version: kotlin_version
    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-jdk8', version: '0.22.2'
    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '0.22.2'
    compile 'org.slf4j:slf4j-simple:1.7.21'
    compile 'io.github.microutils:kotlin-logging:1.4.9'
    compile "commons-codec:commons-codec:1.11"
    compile "commons-codec:commons-codec:1.11:javadoc"
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}

junitPlatform {
    platformVersion = junit_platform_version
}

compileKotlin {
    kotlinOptions.jvmTarget = "$jvm_version"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "$jvm_version"
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

// Important: All classes containing test cases must match the
// the regex pattern "^.*Tests?$" to be picked up by the junit-gradle plugin.
sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.kotlin.srcDirs += 'src/test/kotlin'
}

task run(type: JavaExec) {
    main = 'com.mantono.pyttipanna.GeneratePasswordKt'
    classpath = sourceSets.main.runtimeClasspath
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jar {
    manifest {
        attributes 'Main-Class': 'com.mantono.pyttipanna.GeneratePasswordKt'
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.mantono.pyttipanna.GeneratePasswordKt'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts for faster execution'
    gradleVersion = '4.5'
}